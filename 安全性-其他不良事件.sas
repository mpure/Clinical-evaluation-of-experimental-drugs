/*其他不良事件分析*/
/*运行本程序前请将CDISC数据命名为medical逻辑库，并运行分析集划分.sas*/

PROC SQL;
/*选出受试者ID及对应的不良事件的信息*/
CREATE TABLE AE_FAS0 AS
	(SELECT A.*,F.ACTARMCD FROM MEDICAL.AE A,FAS F
		WHERE A.EPOCH = '双盲治疗期' 
		AND A.USUBJID = F.USUBJID)
	OUTER UNION CORR
	(SELECT F.USUBJID,F.ACTARMCD FROM FAS F
		WHERE F.USUBJID NOT IN (SELECT USUBJID FROM MEDICAL.AE A WHERE A.EPOCH = '双盲治疗期'));
PROC SORT DATA=AE_FAS0;
	BY USUBJID AESEQ;
RUN;
DATA AE_FAS0;
	SET AE_FAS0;
	IF AELLTCD ^='' THEN AELLTCD0=COMPRESS('N'||AELLTCD);
RUN;
PROC TRANSPOSE DATA = AE_FAS0 OUT = TEMP0(DROP=_NAME_ _LABEL_);
	BY USUBJID AESEQ ACTARMCD;
	ID AELLTCD0;
	VAR AEREL;
RUN;

DATA TEMP1 TEMP2;
	SET TEMP0;
	IF AESEQ = 1 THEN OUTPUT TEMP1;
	ELSE  OUTPUT TEMP2;
	DROP AESEQ;
RUN;
PROC SORT DATA=TEMP1;
	BY USUBJID;
RUN;
PROC SORT DATA=TEMP2;
	BY USUBJID;
RUN;
DATA AE_FAS;
	UPDATE TEMP1 TEMP2;
	BY USUBJID;
RUN;

PROC TRANSPOSE DATA = AE_FAS OUT = TEMP3(RENAME=(COL1=AEREL)) NAME=AELLTCD0;
	BY USUBJID ACTARMCD;
	VAR N10021871--N10036446;
RUN;

DATA AE_FASD;
	SET TEMP3;
	IF AEREL='' THEN AEREL='未发生';
RUN;
/*数量统计*/
PROC TABULATE DATA=AE_FASD OUT=NHYP(DROP=_TYPE_ _PAGE_ _TABLE_) MISSING;
	CLASS AELLTCD0 ACTARMCD AEREL;
	TABLE AELLTCD0,ACTARMCD,AEREL;
RUN;

PROC FREQ DATA=NHYP;
/*卡方检验*/
	TABLE ACTARMCD*AEREL/CHISQ CMH FISHER;
	WEIGHT N;
	BY AELLTCD0;
RUN;
