/*生化指标异常分析*/
/*运行本程序前请将CDISC数据命名为medical逻辑库，并运行分析集划分.sas*/

DATA TEMP1;
	SET MEDICAL.LB;
	WHERE LBCLIS IS NOT MISSING;
	KEEP USUBJID LBTEST VISIT EPOCH LBCLIS;
RUN;

PROC SQL;
	CREATE TABLE TEMP2 AS
	SELECT A.*,B.ACTARMCD FROM TEMP1 A,SS B WHERE
	A.USUBJID = B.USUBJID;
QUIT;

PROC SORT DATA=TEMP2 NODUPKEY;
	BY USUBJID LBTEST;
RUN;

PROC TRANSPOSE DATA = TEMP2(DROP=EPOCH) OUT = TEMP3;
	BY USUBJID LBTEST ACTARMCD;
	ID VISIT;
	VAR LBCLIS;
RUN;

PROC SQL;
	CREATE TABLE UNN0 AS
	SELECT * FROM TEMP1 A WHERE
	A.LBCLIS='异常，有临床意义' 
	AND A.EPOCH='双盲治疗期'
	AND A.USUBJID NOT IN 
		(SELECT USUBJID FROM TEMP1 B WHERE
		A.USUBJID = B.USUBJID
		AND A.LBTEST =B.LBTEST
		AND B.EPOCH ='导入期'
		AND B.LBCLIS ='异常，有临床意义');
QUIT;

DATA UNN;
	SET UNN0;
	RESULT='出现异常';
RUN;

PROC SORT DATA=UNN;
	BY USUBJID LBTEST;
RUN;

DATA TEMP4(DROP=VISIT EPOCH);
	UPDATE TEMP3 UNN;
	BY USUBJID LBTEST;
RUN;

DATA TEMP5;
	SET TEMP4;
	IF RESULT='' THEN RESULT='无异常';
RUN;

PROC TABULATE DATA=TEMP5 OUT=LB_STAT(DROP=_TYPE_ _PAGE_ _TABLE_) MISSING;
	CLASS LBTEST ACTARMCD RESULT;
	TABLE LBTEST,ACTARMCD,RESULT;
RUN;

PROC FREQ DATA=LB_STAT;
/*卡方检验*/
	TABLE ACTARMCD*RESULT/CHISQ CMH FISHER;
	WEIGHT N;
	BY LBTEST;
RUN;
