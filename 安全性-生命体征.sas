/*生命体征异常分析*/
/*运行本程序前请将CDISC数据命名为medical逻辑库，并运行分析集划分.sas*/

DATA VS_SS0;
/*选出受试者ID及对应的生命体征的信息*/
	SET MEDICAL.VS;
	WHERE VSTEST IN ('心率','收缩压','舒张压','体温');
RUN;

DATA TEMP1;
	SET VS_SS0;
	LENGTH RESULT $20.;
	IF (VSTEST='心率' AND 50<VSSTRESN<100)
	OR (VSTEST='收缩压' AND 90<VSSTRESN<139)
	OR (VSTEST='舒张压' AND 60<VSSTRESN<89)
	OR (VSTEST='体温' AND 35.4<VSSTRESN<37.7)
	THEN RESULT='正常';
	ELSE RESULT='异常';
	KEEP USUBJID VSTEST VISIT EPOCH RESULT;
RUN;

PROC SQL;
	CREATE TABLE VS_SS AS
	SELECT A.*,B.ACTARMCD FROM VS_SS0 A,SS B WHERE
	A.USUBJID = B.USUBJID;
QUIT;

PROC SORT DATA=TEMP1;
	BY USUBJID VISIT EPOCH;
RUN;

PROC TRANSPOSE DATA = TEMP1 OUT = TEMP2(DROP=_NAME_);
	BY USUBJID VISIT EPOCH;
	ID VSTEST;
	VAR RESULT;
RUN;

PROC SORT DATA = VS_SS(KEEP=USUBJID VSTEST VISIT EPOCH VSSTRESN ACTARMCD);
	BY USUBJID VSTEST ACTARMCD;
RUN;

PROC TRANSPOSE DATA = VS_SS(DROP=EPOCH) OUT = TEMP3(KEEP=USUBJID VSTEST ACTARMCD);
	BY USUBJID VSTEST ACTARMCD;
	ID VISIT;
	VAR VSSTRESN;
RUN;

PROC SQL;
	CREATE TABLE UNN0 AS
	SELECT * FROM TEMP1 A WHERE
	A.RESULT='异常' 
	AND A.EPOCH='双盲治疗期'
	AND A.USUBJID NOT IN 
		(SELECT USUBJID FROM TEMP1 B WHERE
		A.USUBJID = B.USUBJID
		AND A.VSTEST =B.VSTEST
		AND B.EPOCH ='导入期'
		AND B.RESULT ='异常');
QUIT;

DATA UNN;
	SET UNN0;
	RESULT='出现异常';
RUN;

PROC SORT DATA=UNN;
	BY USUBJID VSTEST;
RUN;

DATA TEMP4(DROP=VISIT EPOCH);
	UPDATE TEMP3 UNN;
	BY USUBJID VSTEST;
RUN;

DATA TEMP5;
	SET TEMP4;
	IF RESULT='' THEN RESULT='无异常';
RUN;

PROC TABULATE DATA=TEMP5 OUT=VS_STAT(DROP=_TYPE_ _PAGE_ _TABLE_) MISSING;
	CLASS VSTEST ACTARMCD RESULT;
	TABLE VSTEST,ACTARMCD,RESULT;
RUN;

PROC SORT DATA=VS_STAT;
	BY VSTEST;
RUN;

PROC FREQ DATA=VS_STAT;
/*卡方检验*/
	TABLE ACTARMCD*RESULT/CHISQ CMH FISHER;
	WEIGHT N;
	BY VSTEST;
RUN;
